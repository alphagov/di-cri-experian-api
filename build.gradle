plugins {
	id 'idea'
	id 'application'
	id 'java'
	id "jacoco"
	id "com.diffplug.spotless" version "6.0.4"
	id "org.sonarqube" version "3.3"
}

group "uk.gov.di.ipv.cri"

repositories {
	mavenCentral()
}

sourceCompatibility = 11

dependencies {
	implementation "com.amazonaws:aws-lambda-java-core:1.2.1",
			"com.amazonaws:aws-lambda-java-events:3.11.0",
			"com.fasterxml.jackson.core:jackson-databind:2.13.0",
			"com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.0",
			"org.slf4j:slf4j-simple:1.7.32",
			"org.hibernate.validator:hibernate-validator:6.2.1.Final",
			"org.hibernate:hibernate-validator-annotation-processor:6.2.1.Final",
			"javax.validation:validation-api:2.0.1.Final",
			"javax.el:javax.el-api:3.0.0",
			"org.glassfish:javax.el:3.0.0"

	testImplementation "org.junit.jupiter:junit-jupiter:5.8.1",
			"org.mockito:mockito-junit-jupiter:4.1.0"
}

test {
	useJUnitPlatform ()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.enabled true
	}
}

spotless {
	java {
		target "src/**/*.java"
		googleJavaFormat("1.13.0").aosp()
		importOrder "", "javax", "java", "\\#"
		endWithNewline()
	}
	groovyGradle {
		target '**/*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

run {
	debugOptions {
		enabled = true
		port = 5009
		server = true
		suspend = false
	}
}

sonarqube {
	properties {
		property "sonar.projectKey", "alphagov_di-cri-experian-api"
		property "sonar.organization", "alphagov"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}
